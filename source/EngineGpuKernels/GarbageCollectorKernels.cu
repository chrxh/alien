#include "GarbageCollectorKernels.cuh"

__global__ void cudaPreparePointerArraysForCleanup(SimulationData data)
{
    data.tempObjects.particles.reset();
    data.tempObjects.cells.reset();
}

__global__ void cudaPrepareHeapForCleanup(SimulationData data)
{
    data.tempObjects.heap.reset();
}

__global__ void cudaCleanupCellsStep1(Array<Cell*> cells, Heap newHeap)
{
    // Assumes that cellPointers are already cleaned up
    PartitionData cellPartition = calcAllThreadsPartition(cells.getNumEntries());

    int numCellsToCopy = cellPartition.numElements();
    if (numCellsToCopy > 0) {
        auto newCells = newHeap.getTypedSubArray<Cell>(numCellsToCopy);
        auto newHeapStart = newHeap.getArray();

        int newCellIndex = 0;
        for (int index = cellPartition.startIndex; index <= cellPartition.endIndex; ++index) {
            auto& cell = cells.at(index);
            auto newCell = &newCells[newCellIndex];
            *newCell = *cell;

            cell->tempValue.as_uint64 = reinterpret_cast<uint8_t*>(newCell) - newHeapStart;  // Save index of new cell in old cell
            cell = newCell;

            ++newCellIndex;
        }
    }
}

__global__ void cudaCleanupCellsStep2(Array<Cell*> cellPointers, Heap newHeap)
{
    {
        auto partition = calcAllThreadsPartition(cellPointers.getNumEntries());
        auto newHeapStart = newHeap.getArray();
        for (int index = partition.startIndex; index <= partition.endIndex; ++index) {
            auto& cell = cellPointers.at(index);
            for (int i = 0; i < cell->numConnections; ++i) {
                auto& connectedCell = cell->connections[i].cell;
                connectedCell = reinterpret_cast<Cell*>(newHeapStart + connectedCell->tempValue.as_uint64);
            }
            if (cell->cellType == CellType_Constructor) {
                cell->cellTypeData.constructor.offspring = nullptr;
            }
        }
    }
}

namespace
{
    __device__ void copyAndAssignNewHeapData(uint8_t*& source, uint64_t numBytes, Heap& target)
    {
        if (numBytes > 0) {
            uint8_t* bytes = target.getRawSubArray(numBytes);
            for (uint64_t i = 0; i < numBytes; ++i) {
                bytes[i] = source[i];
            }
            source = bytes;
        }
    }
}

__global__ void cudaCleanupDependentCellData(Array<Cell*> cells, Heap newHeap)
{
    auto const partition = calcAllThreadsPartition(cells.getNumEntries());

    for (int index = partition.startIndex; index <= partition.endIndex; ++index) {
        auto& cell = cells.at(index);
        if (cell->neuralNetwork) {
            copyAndAssignNewHeapData(
                reinterpret_cast<uint8_t*&>(cell->neuralNetwork), sizeof(*cell->neuralNetwork), newHeap);
        }
    }
}

__global__ void cudaCleanupMaps(SimulationData data)
{
    data.cellMap.cleanup_system();
    data.particleMap.cleanup_system();
}

__global__ void cudaSwapPointerArrays(SimulationData data)
{
    data.objects.particles.swapContent(data.tempObjects.particles);
    data.objects.cells.swapContent(data.tempObjects.cells);
}

__global__ void cudaSwapHeaps(SimulationData data)
{
    data.objects.heap.swapContent(data.tempObjects.heap);
}


__global__ void cudaCleanupParticles(Array<Particle*> particlePointers, Heap newHeap)
{
    // Assumes that particlePointers are already cleaned up
    auto partition = calcAllThreadsPartition(particlePointers.getNumEntries());

    int numParticlesToCopy = partition.numElements();
    if (numParticlesToCopy > 0) {
        auto newParticles = newHeap.getTypedSubArray<Particle>(numParticlesToCopy);

        int newParticleIndex = 0;
        for (int index = partition.startIndex; index <= partition.endIndex; ++index) {
            auto& particlePointer = particlePointers.at(index);
            auto& newParticle = newParticles[newParticleIndex];
            newParticle = *particlePointer;
            particlePointer = &newParticle;

            ++newParticleIndex;
        }
    }
}

__global__ void cudaCleanupCreaturesStep1(Array<Cell*> cells)
{
    PartitionData cellPartition = calcAllThreadsPartition(cells.getNumEntries());

    for (int index = cellPartition.startIndex; index <= cellPartition.endIndex; ++index) {
        auto& cell = cells.at(index);
        if (cell->creature) {
            cell->creature->creatureIndex = VALUE_NOT_SET_UINT64;
            if (cell->creature->genome) {
                cell->creature->genome->genomeIndex = VALUE_NOT_SET_UINT64;
            }
        }
    }
}

__global__ void cudaCleanupCreaturesStep2(Array<Cell*> cells, Heap newHeap)
{
    PartitionData cellPartition = calcAllThreadsPartition(cells.getNumEntries());

    for (int index = cellPartition.startIndex; index <= cellPartition.endIndex; ++index) {
        auto& cell = cells.at(index);
        
        if (cell->creature) {
            auto origCreatureIndex = alienAtomicExch64(&cell->creature->creatureIndex, static_cast<uint64_t>(0));  // 0 = member is currently initialized
            if (origCreatureIndex == VALUE_NOT_SET_UINT64) {
                auto newCreature = newHeap.getTypedSubArray<Creature>(1);
                auto const& creature = cell->creature;

                *newCreature = *creature;
                
                // Handle genome sharing: check if this genome has already been copied
                auto origGenomeIndex = alienAtomicExch64(&creature->genome->genomeIndex, static_cast<uint64_t>(0));  // 0 = genome is currently being initialized
                if (origGenomeIndex == VALUE_NOT_SET_UINT64) {
                    // First creature with this genome - copy the genome
                    newCreature->genome = newHeap.getTypedSubArray<Genome>(1);
                    *newCreature->genome = *creature->genome;

                    auto newGenes = newHeap.getTypedSubArray<Gene>(creature->genome->numGenes);
                    newCreature->genome->genes = newGenes;

                    for (int i = 0, j = creature->genome->numGenes; i < j; ++i) {
                        auto const& gene = &creature->genome->genes[i];
                        auto newGene = &newGenes[i];
                        *newGene = *gene;

                        auto newNodes = newHeap.getTypedSubArray<Node>(gene->numNodes);
                        newGene->nodes = newNodes;

                        for (int i = 0, j = gene->numNodes; i < j; ++i) {
                            auto const& node = &gene->nodes[i];
                            auto newNode = &newNodes[i];
                            *newNode = *node;
                        }
                    }
                    auto newGenomeIndex = static_cast<uint64_t>(reinterpret_cast<uint8_t*>(newCreature->genome) - newHeap.getArray());
                    alienAtomicExch64(&creature->genome->genomeIndex, newGenomeIndex);
                } else {
                    // Genome is being processed or already processed by another thread
                    // Wait for genome processing to complete and reuse the genome
                    while (origGenomeIndex == 0) {
                        origGenomeIndex = alienAtomicAdd64(&creature->genome->genomeIndex, static_cast<uint64_t>(0));
                    }
                    // Reuse the already copied genome
                    newCreature->genome = &newHeap.atType<Genome>(origGenomeIndex);
                }
                
                auto newCreatureIndex = static_cast<uint64_t>(reinterpret_cast<uint8_t*>(newCreature) - newHeap.getArray());
                alienAtomicExch64(&cell->creature->creatureIndex, newCreatureIndex);
            } else if (origCreatureIndex != 0) {
                alienAtomicExch64(&cell->creature->creatureIndex, origCreatureIndex);
            }
        }
    }
}

__global__ void cudaCleanupCreaturesStep3(Array<Cell*> cells, Heap newHeap)
{
    PartitionData cellPartition = calcAllThreadsPartition(cells.getNumEntries());

    for (int index = cellPartition.startIndex; index <= cellPartition.endIndex; ++index) {
        auto& cell = cells.at(index);
        if (cell->creature) {
            cell->creature = &newHeap.atType<Creature>(cell->creature->creatureIndex);
        }
    }
}

__global__ void cudaCheckIfCleanupIsNecessary(SimulationData data, bool* result)
{
    if (data.objects.heap.getNumEntries() > data.objects.heap.getCapacity() * Const::ArrayFillPercentage) {
        *result = true;
    } else {
        *result = false;
    }
}
